apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion


    defaultConfig {
        minSdkVersion 9
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                BAIDU_MAP_KEY: '${BAIDU_MAP_KEY}',
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
//            manifest.srcFile 'AndroidManifest.xml'
//            jniLibs.srcDir 'libs'
            assets.srcDir 'assets'
        }
    }
}
def baiduMapSdkVersion = 'v3.0.2'
def depsType = rootProject.properties.get('depsType')
if (depsType == 'mavenLocal') baiduMapSdkVersion += '-SNAPSHOT'

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    if (depsType == 'project') {
        api project(':location')
        api project(':map')
        api project(':panorama')
    } else {
        api "com.github.ipcjs.baidu-map-sdk:location:${baiduMapSdkVersion}"
        api "com.github.ipcjs.baidu-map-sdk:map:${baiduMapSdkVersion}"
        api "com.github.ipcjs.baidu-map-sdk:panorama:${baiduMapSdkVersion}"
    }
}

apply plugin: 'maven-publish'

group = 'com.github.ipcjs'
version = 'g01_3.0.2-SNAPSHOT'
project.archivesBaseName = 'baidu-map-sdk'
task javadocJar(type: Jar) {
    // 注意, 因为这几个目录本来是独立的javadoc文件, 像这样直接复制到一起后, 会导致一些文件被覆盖...
    from '../libs/doc/panorama', '../libs/doc/navi', '../libs/doc/location', '../libs/doc/map', '../libs/doc/ar'
    archiveClassifier.set('javadoc')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact javadocJar
            }
        }
    }
}
